"""
Author: Miquel Anglada Girotto
Contact: miquelangladagirotto [at] gmail [dot] com

Outline
-------
1. Protein activity vs sample: splicing factor programs
"""

import os

# variables
ROOT = os.path.dirname(os.path.dirname(os.getcwd()))
RAW_DIR = os.path.join(ROOT,"data","raw")
PREP_DIR = os.path.join(ROOT,"data","prep")
SUPPORT_DIR = os.path.join(ROOT,"support")
BIN_DIR = os.path.join(ROOT,"bin")
RESULTS_DIR = os.path.join(ROOT,"results","sf_activity_vastdb")
ACTVAL_DIR = os.path.join(ROOT,"results","validation_activity")
SAVE_PARAMS = {"sep":"\t", "index":False, "compression":"gzip"}

##### RULES #####
rule all:
    input:
        # calculate signature
        os.path.join(RESULTS_DIR,"files","signatures","VastDB-PSI_TABLE.tsv.gz"),
        
        # compute viper SF activities
        os.path.join(RESULTS_DIR,"files","protein_activity","VastDB-PSI_TABLE.tsv.gz")
        
        
rule compute_signature:
    input:
        splicing = os.path.join(RAW_DIR,"VastDB","PSI_TABLE-hg38-minN_1-minSD_0-noVLOW-min_ALT_use25-Tidy.tab.gz")
    output:
        signature = os.path.join(RESULTS_DIR,"files","signatures","VastDB-PSI_TABLE.tsv.gz")
    run:
        import pandas as pd
        
        splicing = pd.read_table(input.splicing, index_col=0)
        
        signature = splicing - splicing.median(axis=1).values.reshape(-1,1)
        
        signature.reset_index().to_csv(output.signature, **SAVE_PARAMS)
        
        print("Done!")
       
        
rule compute_protein_activity:
    input:
        signature = os.path.join(RESULTS_DIR,"files","signatures","VastDB-PSI_TABLE.tsv.gz"),
        regulons_dir = os.path.join(ACTVAL_DIR,"files","subsetted_regulons","regulons_selected")
    output:
        os.path.join(RESULTS_DIR,"files","protein_activity","VastDB-PSI_TABLE.tsv.gz")
    params:
        script_dir = BIN_DIR
    shell:
        """
        Rscript {params.script_dir}/compute_protein_activity.R \
                    --signature_file={input.signature} \
                    --regulons_dir={input.regulons_dir} \
                    --output_file={output}
        """
